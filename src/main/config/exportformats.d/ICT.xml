<?xml version="1.0" encoding="UTF-8"?>
<!--
Copyright (C) 2016 Bengt Martensson

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
-->

<exportformats:exportformat xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                            xmlns:exportformats="http://www.harctoolbox.org/exportformats"
                            xsi:schemaLocation="http://www.harctoolbox.org/exportformats http://www.harctoolbox.org/schemas/exportformats.xsd"
                            name="ICT" extension="ict" multiSignal="true" simpleSequence="false">
    <xsl:stylesheet xmlns:girr="http://www.harctoolbox.org/Girr"
                    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
        <xsl:output method="text" />

        <exportformats:documentation>
        <div>This export format generates a text file in ICT format, containing the commands of the export.
            (ICT is the native format of IrScope. It can also be read by IrTransmogrifier.)
        </div>
    </exportformats:documentation>

    <xsl:template match="/girr:remotes">
            <xsl:text>irscope 0
carrier_frequency </xsl:text>
            <xsl:value-of select="girr:remote/girr:commandSet/girr:command/girr:raw/@frequency"/>
            <xsl:text>
</xsl:text>
            <xsl:text>sample_count </xsl:text>
            <xsl:value-of select="count(girr:remote/girr:commandSet/girr:command/girr:raw/*/*)"/>
            <xsl:text>
</xsl:text>
            <xsl:apply-templates select="//girr:command"/>
        </xsl:template>

        <xsl:template match="girr:command">
            <xsl:text>note=</xsl:text>
            <xsl:value-of select="@name"/>
            <xsl:text>
</xsl:text>
            <xsl:apply-templates select="girr:raw[1]/*"/>
        </xsl:template>

        <xsl:template match="girr:intro|girr:repeat|girr:ending">
            <xsl:apply-templates select="*"/>
        </xsl:template>

        <xsl:template match="girr:flash">
            <xsl:text>+</xsl:text>
            <xsl:value-of select="."/>
            <xsl:text>,</xsl:text>
            <xsl:value-of select="round(number(.) * number(../../@frequency div 1000000))"/>
            <xsl:text>
</xsl:text>
        </xsl:template>

        <xsl:template match="girr:gap">
       <xsl:text>-</xsl:text>
            <xsl:value-of select="."/>
            <xsl:text>
</xsl:text>
        </xsl:template>

        <!-- There is a bug/design flaw in IrScope that makes it want to bundle all
        signals into one gigantic macro. Using brute force to circumvent... -->
        <xsl:template match="girr:gap[position()=last()]">
     <xsl:text>-500000
</xsl:text>
        </xsl:template>

    </xsl:stylesheet>
</exportformats:exportformat>