<?xml version="1.0" encoding="UTF-8"?>
<!--
Copyright (C) 2019 Bengt Martensson

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
-->

<exportformats:exportformat xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                            xmlns:exportformats="http://www.harctoolbox.org/exportformats"
                            xsi:schemaLocation="http://www.harctoolbox.org/exportformats https://www.harctoolbox.org/schemas/exportformats.xsd"
                            name="HTML" extension="html" multiSignal="true" simpleSequence="false">

    <xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0"
                    xmlns:html="http://www.w3.org/1999/xhtml"
                    xmlns:girr="http://www.harctoolbox.org/Girr">
        <xsl:output method="html"/>

        <xsl:template match="/">
            <html>
                <head>
                    <title>
                        <xsl:value-of select="girr:remotes/@title"/>
                    </title>
                </head>
                <body>
                    <h1>
                        <xsl:value-of select="girr:remotes/@title"/>
                    </h1>
                    <xsl:apply-templates select="girr:remotes/girr:remote"/>
                </body>
            </html>
        </xsl:template>

        <xsl:template match="girr:remote">
            <h2>Remote: <xsl:value-of select="@name"/></h2>
            <xsl:apply-templates select="girr:notes"/>
            <xsl:apply-templates select="html:img"/>
            <xsl:apply-templates select="girr:commandSet"/>
        </xsl:template>

        <xsl:template match="girr:notes">
            <xsl:copy-of select="."/>
        </xsl:template>

        <xsl:template match="html:img">
            <img>
                <xsl:copy-of select="@*"/>
            </img>
        </xsl:template>

        <xsl:template match="girr:command">
            <h3>
                <xsl:value-of select="@name"/>
                <xsl:apply-templates select="girr:parameters"/>
                <xsl:apply-templates select="@F"/>
            </h3>
            <p>
                <xsl:value-of select="girr:ccf"/>
            </p>

        </xsl:template>

        <xsl:template match="girr:commandSet">
            <xsl:apply-templates select="girr:parameters"/>
            <xsl:apply-templates select="girr:command"/>
        </xsl:template>

        <xsl:template match="girr:commandSet/girr:parameters">
            <xsl:text>Common parameters: </xsl:text>
            <xsl:apply-templates select="@protocol"/>
            <xsl:apply-templates select="girr:parameter"/>
        </xsl:template>

        <xsl:template match="girr:command/girr:parameters">
            (<xsl:apply-templates select="@protocol"/>
            <xsl:apply-templates select="girr:parameter"/>)
        </xsl:template>

        <xsl:template match="@protocol">
            <xsl:text>Protocol=</xsl:text>
            <xsl:value-of select="."/>
        </xsl:template>

        <xsl:template match="@F">
            <xsl:text> F=</xsl:text>
            <xsl:value-of select="."/>
        </xsl:template>

        <xsl:template match="girr:parameter">
            <xsl:text> </xsl:text>
            <xsl:value-of select="@name"/>
            <xsl:text>=</xsl:text>
            <xsl:value-of select="@value"/>
        </xsl:template>

    </xsl:stylesheet>
</exportformats:exportformat>
