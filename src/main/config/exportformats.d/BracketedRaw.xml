<?xml version="1.0" encoding="UTF-8"?>
<!--
Copyright (C) 2019 Bengt Martensson

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
-->

<exportformats:exportformat xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                            xmlns:exportformats="http://www.harctoolbox.org/exportformats"
                            xsi:schemaLocation="http://www.harctoolbox.org/exportformats http://www.harctoolbox.org/schemas/exportformats.xsd"
                            name="BracketedRaw" extension="txt" multiSignal="true" simpleSequence="false">
    <xsl:stylesheet xmlns:girr="http://www.harctoolbox.org/Girr"
                    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
        <xsl:output method="text" />

        <exportformats:documentation>
            <div>This export format generates an text file of the command(s) in the
                <a href="http://www.harctoolbox.org/IrpTransmogrifier.html#BracketedRawFormat">bracketed raw text format</a>.
            </div>
            <div>Limitations:
                <ul>
                    <li>Contains only a set of the commands; the containing <em>remote</em>(s) and their meta data are ignored,</li>
                </ul>
            </div>
        </exportformats:documentation>

        <xsl:template match="/">
            <xsl:apply-templates select="//girr:command"/>
        </xsl:template>

        <xsl:template match="girr:command">
            <xsl:value-of select="@name"/>
            <xsl:text>
</xsl:text>
            <xsl:apply-templates select="girr:raw[1]"/>
            <xsl:if test="position()!=last()">
                <xsl:text>
</xsl:text>
            </xsl:if>
        </xsl:template>

        <xsl:template match="girr:raw">
            <xsl:apply-templates select="@frequency"/>
            <xsl:if test="not(girr:intro)">
                <xsl:text>[]</xsl:text>
            </xsl:if>
            <xsl:apply-templates select="girr:intro[1]"/>
            <xsl:if test="not(girr:repeat)">
                <xsl:text>[]</xsl:text>
            </xsl:if>
            <xsl:apply-templates select="girr:repeat[1]"/>
            <xsl:apply-templates select="girr:ending[1]"/>
            <xsl:text>
</xsl:text>
        </xsl:template>

        <xsl:template match="@frequency">
            <xsl:text>Freq=</xsl:text>
            <xsl:value-of select="."/>
            <xsl:text>Hz</xsl:text>
        </xsl:template>

        <xsl:template match="girr:intro|girr:repeat|girr:ending">
            <xsl:text>[</xsl:text>
            <xsl:apply-templates select="*"/>
            <xsl:text>]</xsl:text>
        </xsl:template>

        <xsl:template match="girr:flash">
            <xsl:text>+</xsl:text>
            <xsl:value-of select="."/>
            <xsl:text>, </xsl:text>
        </xsl:template>
        <xsl:template match="girr:gap">
            <xsl:text>-</xsl:text>
            <xsl:value-of select="."/>
            <xsl:if test="position()!=last()">
                <xsl:text>, </xsl:text>
            </xsl:if>
        </xsl:template>

    </xsl:stylesheet>
</exportformats:exportformat>